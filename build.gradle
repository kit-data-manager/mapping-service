plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.6.6'
    id "org.asciidoctor.jvm.convert" version "3.3.0"
    id "org.owasp.dependencycheck" version "7.0.4.1"
    id 'net.researchgate.release' version '2.8.1'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id 'java'
    id 'jacoco'
}

description = 'Generic mapping service supporting different mapping implementations.'
group = 'edu.kit.datamanager'
version = '0.1.0-SNAPSHOT'

println "Running gradle version: $gradle.gradleVersion"
println "Building ${name} version: ${version}"
println "JDK version: ${JavaVersion.current()}"


configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceCompatibility = 1.17
targetCompatibility = 1.17

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('snippetsDir', file('build/generated-snippets'))
    applicationProperties = System.getProperty('applicationProperties', './src/test/resources/test-config/application-test.properties')
    pythonExecutable = System.getProperty('pythonExecutable', 'file:///usr/bin/python3')
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.3.0.202209071007-r'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.3'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.7.3'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.7.3'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.7.3'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.7.3'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'
    implementation 'org.springdoc:springdoc-openapi-data-rest:1.6.11'
    implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.6.11'
    implementation 'org.javers:javers-spring-boot-starter-sql:6.6.5'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.json:json:20220320'
    implementation 'com.github.jknack:handlebars:4.3.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'edu.kit.datamanager:service-base:1.0.4'
    // apache
    implementation "org.apache.tika:tika-core:2.7.0"

    testImplementation platform('org.junit:junit-bom:5.9.0')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-migrationsupport:5.9.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.3'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.6.RELEASE'
    testImplementation 'org.springframework.security:spring-security-test:5.7.3'
    testImplementation 'org.springframework:spring-test:5.3.23'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'net.bytebuddy:byte-buddy:1.12.16'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.0'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.7.3'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    compileOnly 'org.projectlombok:lombok:1.18.24'

    runtimeOnly 'com.h2database:h2:2.1.214'
    runtimeOnly 'org.postgresql:postgresql:42.5.0'
    runtimeOnly 'org.apache.httpcomponents:httpclient:4.5.13'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.6.RELEASE'
}

test {
    outputs.dir snippetsDir
    finalizedBy jacocoTestReport
    print("Running tests with configuration: ${applicationProperties}")
    print("Running tests with python: ${pythonExecutable}")
    environment 'spring.config.location', applicationProperties
    environment 'pythonLocation', pythonExecutable
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport{
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

jar {
    manifest {
        attributes 'Main-Class': 'edu.kit.datamanager.mapping-service.MappingServiceApplication'
    }
}

springBoot {    
    buildInfo()
}

bootRun {
    systemProperty "spring.config.location", "file:$projectDir/"
    systemProperty "pythonLocation", pythonLocation
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    launchScript()
}

release {
    tagTemplate = 'v${version}'
}

task buildPluginJar(type: Jar) {
 description = 'Bundeling only plugin classes'
 archiveFileName.set("default-plugins-${version}.jar")
 from sourceSets.main.output
 include '**/plugins/impl/*.class'
}
