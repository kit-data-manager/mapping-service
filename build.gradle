plugins {
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
    id 'io.freefair.maven-publish-java' version '8.14.2'
    id "org.owasp.dependencycheck" version "12.1.5"
    id 'net.researchgate.release' version '3.1.0'
    id "com.gorylenko.gradle-git-properties" version "2.5.3"
    id 'io.freefair.lombok' version '8.14.2'
    id 'java'
    id 'application'
    id 'jacoco'
    // plugins for release and publishing to maven repo
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id 'maven-publish'
}

description = 'Generic mapping service supporting different mapping implementations.'
group = 'edu.kit.datamanager'

println "Running gradle version: $gradle.gradleVersion"
println "Building ${name} version: ${version}"
println "JDK version: ${JavaVersion.current()}"


configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

java {
   sourceCompatibility = JavaVersion.VERSION_17
   targetCompatibility = JavaVersion.VERSION_17
}

def buildPofile = project.findProperty("buildProfile") ?: "complete"

if (buildPofile == "minimal") {
    println 'Using minimal profile (no tests) for building ' + project.getName()
    apply from: 'gradle/profile-minimal.gradle'   
} else if (buildPofile == "deploy") {
    println 'Using release profile for building ' + project.getName()
    apply from: 'gradle/profile-deploy.gradle'
}else {
    println 'Using default profile (all tests) for building ' + project.getName()
    apply from: 'gradle/profile-default.gradle'
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('snippetsDir', file('build/generated-snippets'))
    applicationProperties = System.getProperty('applicationProperties', './src/test/resources/test-config/application-test.properties')
    pythonExecutable = System.getProperty('pythonExecutable', "file:///usr/bin/python")
    userDir = System.getProperty('user.dir')
    set('springBootVersion', "3.2.1")
    set('springDocVersion', "2.8.13")
    set('javersVersion', "7.8.6")
    set('keycloakVersion', "19.0.0")
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:7.3.0.202506031305-r'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // monitoring
    implementation 'io.micronaut.micrometer:micronaut-micrometer-registry-prometheus:5.12.0'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.5.5'

     // springdoc
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-common:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:${springDocVersion}"

    // cloud support
    //implementation "org.springframework.cloud:spring-cloud-starter-config:4.1.3"
    //implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:4.1.3"
    //implementation "org.springframework.cloud:spring-cloud-gateway-mvc:4.1.5"

    implementation "org.javers:javers-spring-boot-starter-sql:${javersVersion}"
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'org.apache.maven:maven-artifact:3.9.11'
    implementation 'org.json:json:20250517'
    implementation 'com.github.jknack:handlebars:4.5.0'
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'commons-io:commons-io:2.20.0'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation ('edu.kit.datamanager:service-base:1.3.4'){
        //exclude dependency as spring boot includes 
        //org.glassfish.jaxb:jaxb-core:4.0.5 which leads to a duplication conflict
       	exclude group: "com.sun.xml.bind"
    }
    // apache
    implementation "org.apache.tika:tika-core:3.2.3"
    // JoltPlugin
    implementation "com.bazaarvoice.jolt:jolt-core:0.1.8"

    testImplementation platform('org.junit:junit-bom')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-migrationsupport'
    testImplementation 'org.junit.vintage:junit-vintage-engine'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    //testImplementation 'org.springframework:spring-test'

    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.5'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'net.bytebuddy:byte-buddy:1.17.7'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    runtimeOnly 'com.h2database:h2:2.3.232'
    runtimeOnly 'org.postgresql:postgresql:42.7.8'
    runtimeOnly 'org.apache.httpcomponents:httpclient:4.5.14'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.5'
}

//customization of java compile task
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked']
}

//test configuration
test {
    outputs.dir snippetsDir
    finalizedBy jacocoTestReport
    println("Running tests with configuration: ${applicationProperties}")
    println("Running tests with python: ${pythonExecutable}")
    println("Running tests in directory: ${userDir}")
    environment 'spring.config.location', applicationProperties
    environment 'pythonExecutable', pythonExecutable
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

//jacoco configuration
jacoco {
    toolVersion = "0.8.13"
}

//asciidoctor configuration, i.e., snippet dir
asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

//generation task of boot jar
bootJar {
    dependsOn asciidoctor
    //dependsOn 'buildPluginJar'

     from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.launch.PropertiesLauncher'
    }
    launchScript()
}

//add build info into manifest
springBoot {    
    buildInfo()
}

//properties made available for bootRun task execution
bootRun {
    systemProperty "spring.config.location", "file:$projectDir/"
    systemProperty "pythonLocation", pythonExecutable
}

//define how a new version is tagged and where version information may come from
release {
    tagTemplate = 'v${version}'
    versionPropertyFile = 'gradle.properties'
    versionProperties   = ['version', 'mainversion']
}

//Do not generate entire project jar
tasks.named("jar") {
    enabled = false
}

// Disable generation of Gradle module metadata (.module file)
tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

// task for printing project name.
// should be the last task inside file
task printProjectName {
    doLast {
        println "${project.name}"
    }
}
