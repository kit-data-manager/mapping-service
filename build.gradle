plugins {
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.asciidoctor.jvm.convert' version '4.0.3'
    id 'io.freefair.maven-publish-java' version '8.11'
    id "org.owasp.dependencycheck" version "11.1.0"
    id 'net.researchgate.release' version '3.0.2'
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
    id 'io.freefair.lombok' version '8.11'
    id 'java'
    id 'jacoco'
}

description = 'Generic mapping service supporting different mapping implementations.'
group = 'edu.kit.datamanager'

println "Running gradle version: $gradle.gradleVersion"
println "Building ${name} version: ${version}"
println "JDK version: ${JavaVersion.current()}"


configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

if (System.getProperty('profile') == 'minimal') {
    println 'Using minimal profile for building ' + project.getName()
    apply from: 'gradle/profile-minimal.gradle'   
} else {
    println 'Using default profile executing all tests for building ' + project.getName()
    apply from: 'gradle/profile-complete.gradle'
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('snippetsDir', file('build/generated-snippets'))
    applicationProperties = System.getProperty('applicationProperties', './src/test/resources/test-config/application-test.properties')
    pythonExecutable = System.getProperty('pythonExecutable', 'file:///usr/bin/python')
    userDir = System.getProperty('user.dir')
    set('springBootVersion', "3.2.1")
    set('springDocVersion', "2.6.0")
    set('javersVersion', "7.7.0")
    set('keycloakVersion', "19.0.0")
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:7.0.0.202409031743-r'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

     // springdoc
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-common:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:${springDocVersion}"

    // cloud support
    //implementation "org.springframework.cloud:spring-cloud-starter-config:4.1.3"
    //implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:4.1.3"
    //implementation "org.springframework.cloud:spring-cloud-gateway-mvc:4.1.5"

    implementation "org.javers:javers-spring-boot-starter-sql:${javersVersion}"
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.json:json:20240303'
    implementation 'com.github.jknack:handlebars:4.4.0'
    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'edu.kit.datamanager:service-base:1.3.2'
    // apache
    implementation "org.apache.tika:tika-core:2.9.2"

    testImplementation platform('org.junit:junit-bom')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-migrationsupport'
    testImplementation 'org.junit.vintage:junit-vintage-engine'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    //testImplementation 'org.springframework:spring-test'

    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.3'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'net.bytebuddy:byte-buddy:1.15.10'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    runtimeOnly 'com.h2database:h2:2.3.232'
    runtimeOnly 'org.postgresql:postgresql:42.7.4'
    runtimeOnly 'org.apache.httpcomponents:httpclient:4.5.14'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.3'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked']
}

test {
    outputs.dir snippetsDir
    finalizedBy jacocoTestReport
    println("Running tests with configuration: ${applicationProperties}")
    println("Running tests with python: ${pythonExecutable}")
    println("Running tests in directory: ${userDir}")
    environment 'spring.config.location', applicationProperties
    environment 'pythonExecutable', pythonExecutable
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport{
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

/*jar {
    manifest {
        attributes 'Main-Class': 'edu.kit.datamanager.mapping-service.MappingServiceApplication'
    }
    archiveBaseName = 'mapping-service'
    // version is defined in file 'gradle.properties'
    archiveVersion = System.getenv('version')
    // disable plain jar file
    enabled = false
}*/

bootJar {
    println 'Create bootable jar...'
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.launch.PropertiesLauncher'
    }
    launchScript()
}

springBoot {    
    buildInfo()
}

bootRun {
    systemProperty "spring.config.location", "file:$projectDir/"
    systemProperty "pythonLocation", pythonExecutable
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    launchScript()
}

release {
    tagTemplate = 'v${version}'
}

task buildPluginJar(type: Jar) {
 description = 'Bundeling only plugin classes'
 archiveFileName.set("default-plugins-${version}.jar")
 from sourceSets.main.output
 include '**/plugins/impl/*.class'
}

// task for printing project name.
// should be the last task inside file
task printProjectName {
    doLast {
        println "${project.name}"
    }
}
